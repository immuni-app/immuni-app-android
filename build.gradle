// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        // App version digits
        versionMajor = 2
        versionMinor = 2
        versionPatch = 0

        // Version name follows the <major>.<minor>.<patch> convention
        computeVersionName = { ->
            return String.format('%d.%d.%d', versionMajor, versionMinor, versionPatch)
        }

        // Version code is computed as: major + minor + patch + build number (where available)
        computeVersionCode = { ->
            return (versionMajor * 1000000) + (versionMinor * 100000) + (versionPatch * 10000) + Integer.valueOf(System.env.BUILD_NUMBER ?: 0)
        }

        // App version
        versionName = computeVersionName()
        versionCode = computeVersionCode()

        // SDK and tools
        compileSdkVersion = 30
        minSdkVersion = 23
        targetSdkVersion = 29
        buildToolsVersion = "29.0.3"

        // App dependencies
        kotlinVersion = '1.3.72'
        navigationVersion = "2.3.0"
        koinVersion = "2.1.4"
        moshiVersion = "1.8.0"
        retrofitVersion = "2.6.1"
        okHttpVersion = "4.7.1"
        coroutinesCoreVersion = "1.3.6"
        coroutinesAndroidVersion = "1.3.0"
        appcompatVersion = "1.1.0"
        ktxCoreVersion = "1.2.0"
        securityVersion = "1.0.0-rc02"
        materialVersion = "1.3.0-alpha01"
        glideVersion = "4.9.0"
        lottieVersion = "3.4.0"
        jUnitVersion = "4.12"
        jsonVersion = "20140107"
        mockkVersion = "1.9.3"
        coreTestingVersion = "2.1.0"
        androidxTestVersion = "1.1.1"
        testRunnerVersion = "1.2.0"
        espressoVersion = "3.2.0"
        constraintLayoutVersion = "1.1.3"
        vectordrawableVersion = "1.1.0"
        androidxMediaVersion = "1.1.0"
        fragmentVersion = "1.2.4"
        lifecycleVersion = "2.2.0"
        legacyVersion = "1.0.0"
        recyclerviewVersion = "1.1.0"
        workVersion = "2.3.4"
        viewPager2Version = "1.0.0"
        conscryptVersion = "2.4.0"
        testCoreKtxVersion = "1.2.0"
        testFragmentVersion = "1.2.4"
    }

    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'android.arch.navigation:navigation-safe-args-gradle-plugin:1.0.0'
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.2.1"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    // Version should be inherited from parent
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    // Optionally configure plugin
    ktlint {
        debug = true
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: "org.jlleitschuh.gradle.ktlint"
